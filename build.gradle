
plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.110'
    id 'idea'
    id 'org.jreleaser' version '1.21.0'
}


tasks.named('wrapper', Wrapper).configure {

    distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = mod_group_id

repositories {
    mavenCentral()
}
java {
    withJavadocJar()
    withSourcesJar()
}

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }


    runs {
        client {
            client()

            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()

            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {

            systemProperty 'forge.logging.markers', 'REGISTRIES'

            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {

        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}



sourceSets.main.resources { srcDir 'src/generated/resources' }


configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {

}

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}
sourceSets.main.resources.srcDir generateModMetadata
neoForge.ideSyncTask generateModMetadata

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.github.ren-zaifei'
            artifactId = 'hextech_lib'

            from components.java

            pom {
                name = 'hextech_lib'
                description = 'This is a support library that provides a Hextech-like system.'
                url = 'https://github.com/ren-zaifei/hextech_lib'
                inceptionYear = '2025'
                licenses {
                    license {
                        name = 'LGPL v3'
                        url = 'https://www.gnu.org/licenses/lgpl-3.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'renzaifei'
                        name = '人哉肥'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/ren-zaifei/hextech_lib.git'
                    developerConnection = 'scm:git:ssh://github.com/ren-zaifei/hextech_lib.git'
                    url = 'http://github.com/ren-zaifei/hextech_lib'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}


tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}


tasks.withType(Javadoc).configureEach {
    options.version = true
    options.author = true
    options.encoding = "UTF-8"
    options.charSet = "UTF-8"
}


idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

